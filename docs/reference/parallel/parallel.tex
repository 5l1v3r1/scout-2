\let\clearforchapter\par % cheating, but saves some space


\chapter{Parallel Constructs}

\section{Data-parallel Operations}

\subsection{\texttt{forall} Loops for Meshes}

Building upon the mesh data types, Scout provides support for parallel computations over the 
elements/attributes of the mesh (cells, vertices, etc.).  The majority of these constructs 
use an explicit parallel form that is mixed with the main body of the code that is, by 
definition, executed sequentially.  

Listing~\ref{lst-forall} shows an example parallel 
\texttt{forall} construct.  In this case we are looping over all the cells of the mesh 
\texttt{myMesh} and setting the values of the cell attributes (\texttt{a} and \texttt{b})
to $0.0$.  Note that we can use the specified cell placeholder "\texttt{c}" to directly
access the attributes of the currently active cell using a C-like structure member access
notation.  If there are no clashes within scope, the use of the of explicit cell deferencing
may be dropped within the body of the loop (e.g. \texttt{c.a = 0.0;} can be replaced with
\texttt{a = 0.0f;}).

\par\bigskip
\begin{lstlisting}[float=t,label=lst-forall,
	caption={A \texttt{forall} loop construct.}]
	uniform mesh MeshType {
		cells: float a, b;
	};
  MeshType myMesh[16];

	forall cells c of myMesh { // 'c' is the active cell.
		c.a = c.b = 0.0f;
}
\end{lstlisting}
\par\bigskip\noindent

Note that the code in the body of a \texttt{forall} construct must follow some
constraints.  A field inside a \texttt{forall} construct body may only be
read-only or write-only â€”code that does not satisfy this condition is rejected by the compiler. 
This limitation significantly reduces the complexity of analyzing data dependencies between 
parallel constructs.  Another constraint is that if targeting the GPU, instructions for printing
within the \texttt{forall} construct are not allowed.  Lastly, variables declared outside
of the \texttt{forall} construct body cannot be assigned to within the \texttt{forall}
construct body.

Additional levels of parallelism can be introduced by nesting parallel constructs.  
Specifically, the parallel operations over the cells of a mesh can be combined with  
with a set of operations over the components of each individual cell. Listing~\ref{lst-nestforall} 
shows such a nesting.  Note: this nesting is not implemented yet.

\par\bigskip
\begin{lstlisting}[float=h,label=lst-nestforall,
	caption={Nested \texttt{forall} loop construct over mesh components.}]
forall cells c of myMesh { // 'c' is the active cell.
	forall vertices v of c { // 'v' is the active vertex.
	  c.a += v.a * v.a;
	}
	c.a = sqrt(c.a);
}
\end{lstlisting}
\par\bigskip\noindent

The \texttt{position} attribute of a cell is automatically provided to contain the coordinates of the
current cell being processed. Listing~\ref{lst-position-forall} shows how positions can be
used.

\par\bigskip
\begin{lstlisting}[float=h,label=lst-position-forall,
	caption={Accessing the position of cells within \texttt{forall} loop construct.}]
forall cells c of heat_mesh {
  if (c.position.x > 0 && c.position.x < 1023)
    t1 = 0.0f;
  else
    t1 = MAX_TEMP;      
}
\end{lstlisting}
\par\bigskip\noindent

A shortened version of the \texttt{forall} construct using different syntax is
available.  In Listing~\ref{lst-forallshort}, a two-dimensional mesh can have
a subrange of cells initialized.  Note that \texttt{width}, \texttt{height} and
\texttt{depth} attributes are available for accessing the mesh sizes in each dimension.
The range is specified by \texttt{[}\textit{first-index}\texttt{..}\textit{last-index}\texttt{]}.
Note this is not fully implemented (all elements get initialized 
instead of a subrange).

\par\bigskip
\begin{lstlisting}[float=h,label=lst-forallshort,
	caption={\texttt{forall} short syntax for initialization.}]
float MAX_TEMP = 1000;

uniform mesh MyMesh{
    cells:
      float a;
};

MyMesh m[512,512];

m.a[1..width-2][1..height-2] = MAX_TEMP;
\end{lstlisting}
\par\bigskip\noindent


Built-in functions can allow neighbors of a cell to be referenced.  The \texttt{cshift} 
(circular shift) built-in function is used to access neighboring cells in the mesh.
For now this is implemented for uniform meshes.  This is like F90 but we shift index values 
versus array duplication.  The following is a 2D mesh.  The \texttt{cshift}  function
takes as arguments the cell field, then the index offset in the x, y and z directions depending
on how many dimensions in the mesh.  Listing~\ref{lst-cshiftforall} shows how the \texttt{cshift}
function can be used.

\par\bigskip
\begin{lstlisting}[float=h,label=lst-cshiftforall,
	caption={\texttt{forall} loop construct with use of cshift.}]
forall cells c of heat_mesh {
  if (c.position.x > 0 && c.position.x < heat_mesh.width-1 &&
      c.position.y > 0 && c.position.y < heat_mesh.height-1) {

    float d2dx2 = cshift(c.t1, 1, 0) - 2.0f * c.t1 
                  + cshift(c.t1, -1, 0);
    d2dx2 /= dx * dx;

    float d2dy2 = cshift(c.t1, 0, 1) - 2.0f * c.t1 
                  + cshift(c.t1, 0, -1);
    d2dy2 /= dy * dy;

    t2 = (alpha * dt * (d2dx2 + d2dy2)) + c.t1;
  }
}
\end{lstlisting}
\par\bigskip\noindent

The built-in function \texttt{sten()} also allows access to neighboring cells.  Note:  this is not yet 
implemented.  Listing~\ref{lst-stenforall} shows how the \texttt{sten} function can be used.

\par\bigskip
\begin{lstlisting}[float=h,label=lst-stenforall,
	caption={Nested \texttt{forall} loop construct with selection of neighboring cells.}]
forall cells c of myMesh { // 'c' is the active cell.
	forall n => sten(c) { // forall neighbors 'n' of cell 'c'.
		...
	}
	...
}
\end{lstlisting}
\par\bigskip\noindent

\subsection{\texttt{forall} Loops for Arrays}

The \texttt{forall} construct can also be used to do explicitly parallel computations
on arrays.  The syntax in the \texttt{forall} allows the user to specify
\texttt{[}\textit{first-element-index}\texttt{:}\textit{array-size}\texttt{:}\textit{step-size}\texttt{]}.
An example of a \texttt{forall} array construct for a one-dimensional array is shown in 
Listing~\ref{lst-forall1darray}.

\par\bigskip
\begin{lstlisting}[float=h,label=lst-forall1darray,
	caption={\texttt{forall} loop construct for one-dimensional array.}]
int a[100];

for(size_t i = 0; i < 100; ++i){
  a[i] = i;
}  

forall i in [0:100:1]{
  a[i] = i * 2;
}
\end{lstlisting}
\par\bigskip\noindent

Arrays of up to three dimensions can be looped with the \texttt{forall} construct.
The Listing~\ref{lst-forall3darray} shows an example of a three-dimensional array.

\par\bigskip
\begin{lstlisting}[float=h,label=lst-forall3darray,
	caption={\texttt{forall} loop construct for three-dimensional array.}]
int a[5][5][5];

forall i,j,k in [0:2:1, 0:3:1, 0:4:1]{
  a[i][j][k] = i*100 + j*10 + k;
}
\end{lstlisting}
\par\bigskip\noindent


\section{Queries}

The \texttt{where} keyword allows the user to specify a subset of mesh cells 
to be processed.  Listing~\ref{lst-stream-forall} illustrates the use of
the \texttt{where} keyword.  Note: this is parsed, but not yet implemented.

\par\bigskip
\begin{lstlisting}[float=h,label=lst-stream-forall,
	caption={Streaming \texttt{forall} loop construct using \texttt{where}.}]
// Stream only a subset of cells of myMesh into the loop
// body for processing. 
forall cells c of myMesh where (c.a > 0.0) { 
	c.a = c.b / c.a;
}
\end{lstlisting}
\par\bigskip\noindent

The \texttt{select()} built-in function allows the user to specify a subset of mesh fields
to be processed.  Listing~\ref{lst-stream-forall2} illustrates the use ofthe \texttt{select} 
built-in function.  Note: this is not yet implemented.

\par\bigskip
\begin{lstlisting}[float=h,label=lst-stream-forall2,
	caption={Streaming \texttt{forall} loop construct using \texttt{select}.}]
// Stream only a subset of cells of myMesh into the loop
// body for processing. 
forall cells of myMesh(select(a, b)) { 
	a = b / a;
}
\end{lstlisting}
\par\bigskip\noindent

Expressions using the \texttt{expr} construct can be created to accomplish a similar effect as the
where clause above with the use of \texttt{apply} in the \texttt{forall} construct.  
Listing~\ref{lst-stream-forall3} illustrates the use ofthe \texttt{expr} construct.
Note:  this is not yet implemented.

\par\bigskip
\begin{lstlisting}[float=h,label=lst-stream-forall3,
	caption={Streaming \texttt{forall} loop construct using \texttt{expr}.}]
// Stream only a subset of cells of myMesh into the loop
// body for processing. 
expr e = where cells c of myMesh (a < 0.5);

forall cells of myMesh(apply e) { 
	a = b / a;
}
\end{lstlisting}
\par\bigskip\noindent

